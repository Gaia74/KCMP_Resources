//===========================================================================
//
//  Fire Sentry Gun
//
//===========================================================================

ACTOR Torm_FireSentryGun : Torm_SentryGun 1247
{
	//$Category "Tormentor Monsters"
	
	Tag "Fire Sentry Gun"
	
    Health 125
	Painchance 64
	Mass 1250
    SeeSound "Torm_FireSentryGun/Sight"
    ActiveSound "Torm_FireSentryGun/Active"
    PainSound "Torm_FireSentryGun/Pain"
    DeathSound "Torm_FireSentryGun/Death"
    Obituary "%o was eradicated by a Fire Sentry gun."
	Species "Tech"
	
    States
    {
	Spawn:
        D219 A 1 A_Look
        Loop
    See:
        D219 A 1 A_Chase
        Loop
	Pain:
	    D219 A 8 A_Pain
		Goto See
	Missile: 
		D219 A 0 A_PlaySound("Torm_SentryGun/Alert")
		D219 A 20 A_FaceTarget
		D219 A 0 A_Jumpifcloser(475, "Missile2")
		D219 A 3 A_FaceTarget
		D219 B 0 A_PlaySound("Torm_FireSentryGun/Shot")
		D219 B 1 Bright A_CustomMissile("Torm_SentryGunHitcan1",42,0,FRandom(-8,8))
		D219 B 2 Bright
		D219 A 0 A_MonsterRefire(50, "See")
        Goto Missile+2
    Missile2: 
		D219 A 2 A_FaceTarget
		D219 B 2 Bright A_CustomMissile("Torm_FireSentryGunFlame1",42,0,FRandom(-4,4))
		D219 B 0 A_CustomMissile("Torm_FireSentryGunFlame2",42,0,FRandom(-4,4))
		D219 A 0 A_MonsterRefire(50, "See")
        Goto Missile+2
    Death:
        D218 A 1 A_Scream
		D218 C 0 A_NoBlocking
        D218 C 0 A_PlaySoundEx("Weapons/Rocklx",CHAN_ITEM)
        D218 C 0 A_SpawnItem ("Torm_SentryGunBoom", 0, 42)
        D218 CCCC 0 A_SpawnItemEx("Torm_LandMineExSpark", 0, 0, 42, FRandom(6,-6), FRandom(6,-6), FRandom(8,12))
		D218 CC 0 A_SpawnItemEx("Torm_MetalRandom2", 0, 0, FRandom(42,48), 0, 0, 0, 0, 128)
		D218 CC 0 A_SpawnItemEx("Torm_MetalRandom2", 0,0, FRandom(42,48), random(2,6),random(2,6),random(2,6), random(0,360), 128)
	    D218 CC 0 A_SpawnItemEx("Torm_MetalRandom2", 0,0, FRandom(42,48), random(2,6),random(2,6),random(2,6), random(0,360), 128)
        D218 C 1
        D218 C -1
        Stop
    }
}

ACTOR Torm_FireSentryGunFlame1
{
    Radius 8
    Height 8
    Speed 22
    Damage 2
    Projectile
    RenderStyle Add
	Scale 0.78
    Alpha 0.67
    DamageType "Fire"
    SeeSound "Torm_FireSentryGun/Flame"
    Decal "DoomImpScorch"
	
    States
    {
    Spawn:
        D220 ABCDEFG 2 Bright
        D220 HIJKLMNO 2 Bright
        Stop
    Death:
        D220 HIJKLMNO 1 Bright
        Stop
    }
}

ACTOR Torm_FireSentryGunFlame2 : Torm_FireSentryGunFlame1
{
    SeeSound ""
}