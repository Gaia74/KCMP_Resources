//===========================================================================
//
//  Terminator
//
//===========================================================================

ACTOR Torm_Terminator 1251
{
	//$Category "Tormentor Monsters"
	
	Tag "Terminator"
	
    Health 400
    Mass 575
    Speed 8
    Radius 22 
    Height 54 
    PainChance 64
	SeeSound "Torm_Terminator/Sight"
    ActiveSound "Torm_Terminator/Active"
    PainSound "Torm_Terminator/Pain"
    DeathSound "Torm_Terminator/Death"
    Obituary "%o was liquidated by a Terminator."
	Species "Tech"
    Monster  
	
    +NOBLOOD
	+NOICEDEATH
	+FIRERESIST
    +MISSILEMORE
    +QUICKTORETALIATE
	
	var int user_fire;
    States
    {
    Spawn: 
		D227 AB 10 A_Look
		Loop
    See:
        D227 AABB 4 A_Chase
        TNT1 A 0 A_StartSound("Torm_UACWalker/Step",CHAN_BODY,0,1.0,ATTN_NORM,1.1)
        D227 CCDD 4 A_Chase
        TNT1 A 0 A_StartSound("Torm_UACWalker/Step",CHAN_BODY,0,1.0,ATTN_NORM,1.1)
        Loop 
    Pain:
        TNT1 A 0 { if(user_fire) { user_fire=0; A_PlayWeaponSound("Don_Zombie/ChaingunStop2"); } }
        D227 L 3 	
        D227 L 3 A_Pain
        Goto See
    Missile:
        D227 E 0 A_Jump(96,"CheckFire")
        D227 E 0 A_JumpIfCloser(745,"Chaingun1")
        D227 E 0 A_Jump(64,"Chaingun1")
    CheckFire: 
        D227 E 7 A_FaceTarget
		D227 F 0 A_Playsound("Torm_Terminator/Fire")
        D227 E 0 A_FaceTarget
        D227 F 7 Bright A_CustomMissile("Torm_TerminatorBlaster",38,15,0,0)
        D227 E 7
        D227 E 0 A_Jump(128, 1)
		Goto See
        D227 E 7 A_FaceTarget
		D227 F 0 A_Playsound("Torm_Terminator/Fire")
        D227 E 0 A_FaceTarget
        D227 F 7 Bright A_CustomMissile("Torm_TerminatorBlaster",38,15,0,0)	
        D227 E 7
        D227 E 0 A_Jump(128, 1)	
		Goto See
        D227 E 7 A_FaceTarget
		D227 F 0 A_Playsound("Torm_Terminator/Fire")
        D227 E 0 A_FaceTarget
        D227 F 7 Bright A_CustomMissile("Torm_TerminatorBlaster",38,15,0,0)
        D227 E 7
        D227 E 0 A_Jump(128, 1)	
		Goto See
        D227 E 7 A_FaceTarget
		D227 F 0 A_Playsound("Torm_Terminator/Fire")
        D227 E 0 A_FaceTarget
        D227 F 7 Bright A_CustomMissile("Torm_TerminatorBlaster",38,15,0,0)
        D227 E 7
		Goto See	
	Chaingun1:
        TNT1 A 0 A_SetUserVar(user_fire,0)
        D227 GG 5 A_FaceTarget
    Chaingunning:
        TNT1 A 0 A_SetUserVar(user_fire,user_fire+1)
		D227 G 1 A_FaceTarget
		D227 H 2 Bright { A_CustomMissile("Torm_TerminatorHitcan1",36,-14,FRandom(-8,8),CMF_OFFSETPITCH,FRandom(-2,2)); A_PlayWeaponSound("Don_Zombie/Chaingun"); }
		D227 G 2 A_FaceTarget
		D227 I 2 Bright { A_CustomMissile("Torm_TerminatorHitcan1",36,-14,FRandom(-8,8),CMF_OFFSETPITCH,FRandom(-2,2)); A_PlayWeaponSound("Don_Zombie/Chaingun"); }
		D227 A 0 A_MonsterRefire(40, "ChainStop")
        TNT1 A 0 A_JumpIf(user_fire>=20,"ChainStop")
        Loop
    ChainStop:
        TNT1 A 0 A_SetUserVar(user_fire,0)
        TNT1 A 0 A_PlayWeaponSound("Don_Zombie/ChaingunStop2")
        Goto See
	Death:
        D227 M 6 { A_Scream; A_StopSound(CHAN_WEAPON); }
        D227 N 4 
        D227 OP 4 Bright
        D227 Q 4 Bright A_NoBlocking
		D227 A 0 { A_SpawnItem("KiradorRocketExplosion",0,54); A_QuakeEx(1.4,1.4,1.4,20,0,1024,"",QF_SCALEDOWN); }
		D227 RR 0 A_SpawnItemEx("Torm_MetalRandom", 0, 0, FRandom(24,33), 0, 0, 0, 0, 128)
		D227 RR 0 A_SpawnItemEx("Torm_MetalRandom2", 0, 0, FRandom(24,33), 0, 0, 0, 0, 128)
        D227 R 0 A_SpawnItemEx("Torm_TerminatorHead", 0, 0, 54, FRandom(4,6), FRandom(4,6), FRandom(4,6), FRandom(0,360), 128) 
        D227 R 0 A_SpawnItemEx("Torm_TerminatorShoulder", 0, 0, 49, FRandom(4,6), FRandom(4,6), FRandom(4,6), FRandom(0,360), 128)
        D227 R 4 Bright A_SpawnItemEx("Torm_TerminatorArm", 60, 0, 28, 3, 0, 0, -90, 128)
        D227 STUV 4 Bright
        D227 W 4 
        D227 XYZ 6 
        D227 [ -1 
        Stop 
    }
}

ACTOR Torm_TerminatorHitcan1 : FastProjectile
{
    Damage 2
    Radius 3
    Height 7
    //Speed 150
    Speed 100
    Projectile
    Decal "Bulletchip"
	
    RenderStyle Add
    +BRIGHT
	+STRIFEDAMAGE
	+NOGRAVITY
	+DONTBLAST
	+NOBLOCKMAP
    +DONTREFLECT
    +BLOODSPLATTER
	
    States
    {
    Spawn:
        NULL A 1
        Loop
	Crash:
	Death:
        TNT1 A 0 // pondria un sonido de ricochet aqui... SI TUVIERA UNO !
        TNT1 A 1 A_SpawnItemEx("KiradorTankPuff", 0, 0, 0, 0, 0, 1, 0, SXF_NOCHECKPOSITION)
        Stop
	XDeath:
        TNT1 A 1 A_SpawnItemEx("KiradorTankPuff", 0, 0, 0, 0, 0, 1, 0, SXF_NOCHECKPOSITION)
        Stop
    }
}

ACTOR Torm_TerminatorHitcan2 : Torm_TerminatorHitcan1
{
    Speed 35
}

ACTOR Torm_TerminatorBlaster
{
    Radius 8
    Height 12
    Speed 40
	FastSpeed 47
    Damage 15
    Projectile
    Alpha 0.85
    DeathSound "Torm_Terminator/Hit"
    Obituary "%o was blasted away by a Terminator."
    Decal "CacoScorch"
    //RenderStyle Add
	+STRIFEDAMAGE
	+FIREDAMAGE
	
    States
    {
    Spawn: 
        D231 A 1 Bright 
        Loop
    Death:
        D231 B 6 Bright
        D231 CDEFGHIJKL 3 Bright 
        Stop
    }
}