//===========================================================================
//
//  Horn Beast
//
//===========================================================================

ACTOR Torm_HornBeast 1252
{
	//$Category "Tormentor Monsters"
	
	Tag "Horn Beast"
	
    Health 500
    Mass 800
    Speed 10
    Radius 20 
    Height 60 
    PainChance 32
	BloodColor "08 08 08"
	SeeSound "Torm_HornBeast/Sight"
    ActiveSound "Torm_HornBeast/Active"
    PainSound "Torm_HornBeast/Pain"
    DeathSound "Torm_HornBeast/Death"
    Obituary "%o was burned to crisp by a horn beast."
    Species "Dark"
    Monster  
	
	DamageFactor "Fire2", 0.0
	
	+FLOORCLIP
	+FIRERESIST
    +QUICKTORETALIATE
	
    States
    {
    Spawn: 
		D232 AB 10 Bright A_Look
		Loop
    See:
        D232 AABBCCDD 3 Bright A_Chase
        Loop 
    Missile:
        D232 E 10 Bright A_FaceTarget
		D232 E 0 A_Playsound("Torm_HornBeast/Fire",5)
        D232 E 0 A_CustomMissile("Torm_HornBeastBall", 32, 20, 0)
        D232 E 0 A_CustomMissile("Torm_HornBeastBall", 32, -20, 0)
        D232 E 5 Bright A_FaceTarget
        Goto See
    Pain:
        D232 F 4 Bright	
        D232 F 4 Bright A_Pain
        Goto See
	Death:      	
        D232 G 8 Bright A_Scream
        D232 H 7 Bright 
        D232 I 6 Bright A_NoBlocking
        D232 JK 5 Bright 
        D232 LMNO 4 
        D232 P -1
        Stop
	Raise:
	    D232 ONML 4
	    D232 JKIHG 5
		Goto See
    }
}

ACTOR Torm_HornBeastBall 
{
    Radius 10
    Height 10
    Speed 26
    FastSpeed 32
    Damage 3
	Scale 0.55
	Decal "Scorch"
    DeathSound "Torm_HornBeast/Hit"
	RenderStyle Add
    Projectile
	
    +FIREDAMAGE
	
    States
    {
    Spawn:
        D233 AB 2 Bright A_SpawnItemEx ("Torm_HornBeastBallTrail", 0, 0, 0, 0, 0, 1, 5, 160)
        Loop
    Death:
		D234 A 2 Bright A_SetScale(0.65,0.65)
		D234 BCDEFGHI 1 Bright A_CustomMissile("Torm_HornBeastParticle", 0, 0, FRandom(0,359), CMF_TRACKOWNER|SXF_TRANSFERPOINTERS, Random(0,60))
		D234 JKLMNOPPQQRRSS 1 Bright
		Stop
    }
}

ACTOR Torm_HornBeastBallTrail
{
    Radius 1
    Height 1
    Speed 5
    Damage 0
    Alpha 0.6
	Gravity 0.3
    Renderstyle Add
	
	+MISSILE
	+FIREDAMAGE
	+DONTREFLECT
	+CLIENTSIDEONLY
    +NOEXPLODEFLOOR
	
    States
    {
    Spawn:
        D235 ABCDEFG 2 Bright
    Death:
        D235 G 1 Bright A_Fadeout(0.05)
        Loop
    }
}

ACTOR Torm_HornBeastParticle
{
    Radius 6
    Height 6
    Speed 10
    Damage 1
    Gravity 1.0
    Scale 0.75
    DeathSound "Torm_HornBeast/Blast"
	RenderStyle Add
    Decal "HImpScorch"
	
    +DROPOFF
    +MISSILE
    +NOBLOCKMAP
    +NOTELEPORT
	
    States
    {
    Spawn:
        D236 A 1 Bright A_SpawnItemEx("Torm_HornBeastParticleTrail", 0, 0, 0, 0, 0, 0, 0, 128)
        Loop
    Death:
	    D236 F 0 
	    D236 F 0 A_SetScale(0.58,0.58)
	    D236 F 2 Bright A_Stop
        D236 GHIJ 2 Bright
        Stop
    }
}

ACTOR Torm_HornBeastParticleTrail 
{
    Scale 0.75
    Alpha 0.6
    RenderStyle Add
	
	+NOGRAVITY
    +CLIENTSIDEONLY
	
    States
    {
    Spawn:
	    D236 BCDE 3 Bright A_FadeOut(0.12)
	    Stop
	}
}