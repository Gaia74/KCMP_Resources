//===========================================================================
//
//  Nightshade
//
//===========================================================================

ACTOR Torm_Nightshade : Wizard 1244
{
	//$Category "Tormentor Monsters"
	
	Tag "Nightshade"
	
    Health 60
    Mass 320
	Speed 12
    Radius 18
    Height 56
	Painchance 32
	MeleeRange 50
    SeeSound "Torm_Nightshade/Sight"
    ActiveSound "Torm_Nightshade/Active"
	PainSound "Torm_Nightshade/Pain"
    DeathSound "Torm_Nightshade/Death"
    Obituary "%o couldn't escape by the Nightshade."
    Species "Dark"
    RenderStyle Translucent
	Monster
	
	DamageFactor "Fire2", 0.0
	
	+NOBLOOD
    +NOBLOODDECALS
    +QUICKTORETALIATE
	
    States
    {
    Spawn:
        D213 AB 10 A_Look
        Loop
    See:
		D213 AABBCCDD 3 A_Chase
        Loop
	Melee:
    Missile: 
	    D213 E 0 A_WizAtk2 
		D213 E 8 A_FaceTarget
        D213 F 8 A_CustomComboAttack("Torm_NightshadeMissile", 32, FRandom(1,6)*3, "Torm_Nightshade/Melee")
        D213 B 4 A_GhostOff
        Goto See 
    Pain:
        D213 G 5 A_Pain
        D213 AAAAA 0 A_SpawnItemEx ("Torm_NightshadeSmoke", -2, Random (0, -8), Random (16, 46), 0, Random (-1, 1), Random (-1, 1), 0, 160)
	    TNT1 A 1 A_UnsetShootable
	    TNT1 A 10
    Move:
	    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Wander
	    TNT1 A 0 A_Jump(64,"Move")
        TNT1 AAAAA 0 A_SpawnItemEx("Torm_NightshadeSmoke",-2, Random (0, -8), Random (16, 46), 0, Random (-1, 1), Random (-1, 1), 0, 160)
        D213 G 1 A_PlaySound("Torm_Nightshade/Fade")
		D213 G 2 A_SetTranslucent(1.0,0) 
		D213 G 1 A_GhostOff
        D213 G 1 A_SetShootable
		Goto See
    Death:
        D213 H 4 A_SetTranslucent(1.0,0)
        D213 I 4 A_SetTranslucent(0.9,0)
        D213 I 0 A_Scream
        D213 J 4 A_SetTranslucent(0.8,0)
        D213 K 0 A_NoBlocking
        D213 K 4 A_SetTranslucent(0.7,0)
        D213 L 4 A_SetTranslucent(0.6,0)
        D213 M 4 A_SetTranslucent(0.5,0)
        D213 N 4 A_SetTranslucent(0.4,0)
        D213 O 4 A_SetTranslucent(0.3,0)
        D213 P 2 A_SetTranslucent(0.2,0)
        D213 P 2 A_SetTranslucent(0.1,0)
	    TNT1 A 1 
        Stop
    }
}

ACTOR Torm_NightshadeMissile
{
	Radius 4
	Height 4
    Speed 17
    FastSpeed 22
    Damage 3
    Alpha 0.80
	Projectile
    SeeSound "Torm_Nightshade/Fire"
    DeathSound "Torm_Nightshade/Hit"
	Decal "HImpScorch"
	
    +SEEKERMISSILE
	+RANDOMIZE
	
    States
    {
    Spawn:
        D215 AAB 2 Bright A_SpawnItemEx("Torm_NightshadePuff",0,0,0,0,0,0)
        D215 C 0 A_Seekermissile(5,6) 
        D215 BCC 2 Bright A_SpawnItemEx("Torm_NightshadePuff",0,0,0,0,0,0) 
        Loop
    Death:
        D215 DEFGH 4 bright
        Stop
    }
}

ACTOR Torm_NightshadeSmoke
{
    Radius 2
    Height 2
    Scale 1.5
    Alpha 0.5
    RenderStyle Translucent
	
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +CLIENTSIDEONLY
	
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump (256, "Blot1", "Blot2", "Blot3", "Blot4")
    Blot1:
        D214 A 2 
        D214 B 2 A_SetTranslucent (0.4, 0)
        D214 C 2 A_SetTranslucent (0.3, 0)
        D214 D 2 A_SetTranslucent (0.2, 0)
        D214 E 2 A_SetTranslucent (0.1, 0)
        Stop
    Blot2:
        D214 F 2 
        D214 G 2 A_SetTranslucent (0.4, 0)
        D214 H 2 A_SetTranslucent (0.3, 0)
        D214 I 2 A_SetTranslucent (0.2, 0)
        D214 J 2 A_SetTranslucent (0.1, 0)
        Stop
    Blot3:
        D214 K 2 
        D214 L 2 A_SetTranslucent (0.4, 0)
        D214 M 2 A_SetTranslucent (0.3, 0)
        D214 N 2 A_SetTranslucent (0.2, 0)
        D214 O 2 A_SetTranslucent (0.1, 0)
        Stop
    Blot4:
        D214 P 2 
        D214 Q 2 A_SetTranslucent (0.4, 0)
        D214 R 2 A_SetTranslucent (0.3, 0)
        D214 S 2 A_SetTranslucent (0.2, 0)
        D214 T 2 A_SetTranslucent (0.1, 0)
        Stop
    }
}

ACTOR Torm_NightshadePuff
{
    Radius 1
    Height 1
    Speed 0
    Alpha 0.65
    Projectile
    RenderStyle Add
	
    +NOCLIP
	+CLIENTSIDEONLY
	
    States
    {
    Spawn:
	    TNT1 A 3
        D215 IJKLMN 3 Bright
        Stop       
    }
}