//===========================================================================
//
//  Hierophant
//
//===========================================================================

ACTOR Torm_Hierophant1 1253
{
	//$Category "Tormentor Monsters"
	
	Tag "Flying Hierophant"
	
    Health 700
    Mass 1200
    Speed 10
    Radius 20 
    Height 60 
    PainChance 36
	MeleeRange 60
	SeeSound "Torm_Hierophant/Sight"
    ActiveSound "Torm_Hierophant/Active"
    PainSound "Torm_Hierophant/Pain"
    DeathSound "Torm_Hierophant/Death"
	Species "Flesh"
    Monster  
    Obituary "%o was hexed by a Hierophant."
    HitObituary "%o got shredded by a Hierophant."
	
    +FLOAT
    +FLOORCLIP
    +NOGRAVITY
	+FIRERESIST
    +MISSILEMORE
    +QUICKTORETALIATE	
	
    var int user_i; 
	
    States
    {
    Spawn: 
		D237 AB 8 A_Look
		Loop
    See:
        D237 A 0 A_ChangeFlag("INVULNERABLE",0)
	    D237 A 0 A_ChangeFlag("REFLECTIVE",0)
        D237 AABBAACCDDCC 4 A_Chase
        D237 A 0 A_Jump(35, "Blur")
        Loop 
	Blur:
	    D237 A 0 A_ChangeFlag("INVULNERABLE",1)
	    D237 A 0 A_ChangeFlag("REFLECTIVE",1)
        D237 A 1 A_PlaySound("Torm_FleshWizard/Stealth1",2)
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailB",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailB",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailB",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailB",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailA",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailD",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailD",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailD",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailD",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_SpawnItemEx("Torm_HierophantTrailC",0,0,0,0,0,0,0,128,0)
        D237 A 1 A_Wander
        D237 A 0 A_ChangeFlag("INVULNERABLE",0)
	    D237 A 0 A_ChangeFlag("REFLECTIVE",0)
        Goto See
    Missile:
        D237 A 0 
        D237 A 0 A_Jump(256, "Missile1", "Missile2", "Missile3") 
        D237 A 0 
    Missile1:
        D237 EF 6 A_FaceTarget
        D237 G 0 A_CustomMissile("Torm_HierophantBall1", 26, 0, Random(-6, -1))
        D237 G 6 A_CustomMissile("Torm_HierophantBall1", 26, 0, Random(1, 6))
        D237 A 3
        D237 HI 6 A_FaceTarget
        D237 J 0 A_CustomMissile("Torm_HierophantBall1", 26, 0, Random(-6, -1))
        D237 J 6 A_CustomMissile("Torm_HierophantBall1", 26, 0, Random(1, 6))
        D237 A 3 A_SpidRefire
        D237 A 0 A_Jump(64, "Missile1")
        Goto See
    Missile2:
        D237 EF 6 A_FaceTarget
        D237 G 6 A_CustomMissile("Torm_HierophantBall3", 26)
        D237 A 3 A_SpidRefire
        D237 A 0 A_Jump(128, "Missile1", "Missile3")
        Goto See
    Missile3:
        D237 KLM 6 A_FaceTarget
    Missile3LoopPrep:
        D237 A 0 A_SetUserVar("user_i", 0)
    Missile3Loop:
        D237 N 1 Bright A_CustomMissile("Torm_HierophantBall2")
        D237 O 1 A_FaceTarget
        D237 A 0 A_SetUserVar("user_i", user_i + 1)
        D237 A 0 A_JumpIf(user_i < 15, "Missile3Loop")
        D237 L 6
        Goto See
    Melee:
        D237 A 0 A_ChangeFlag("INVULNERABLE",0)
	    D237 A 0 A_ChangeFlag("REFLECTIVE",0)
        D237 A 0 A_Jump(128, "RightSlash")
        D237 EF 6 A_FaceTarget		
        D237 G 6 A_CustomComboAttack("Torm_HierophantBall1", 26, FRandom(1,9)*8, "Baron/Melee", Normal, 1)
        Goto See
    RightSlash:
        D237 HI 6 A_FaceTarget		
        D237 J 6 A_CustomComboAttack("Torm_HierophantBall1", 26, FRandom(1,9)*8, "Baron/Melee", Normal, 1)
        Goto See    
    Pain:
        D237 P 2
	    D237 P 0 A_ChangeFlag("INVULNERABLE",0)
	    D237 P 0 A_ChangeFlag("REFLECTIVE",0)
        D237 P 2 A_Pain
        Goto See
	Death:	
        D237 Q 6 
        D237 R 6 A_Scream
        D237 S 6 A_NoBlocking
        D237 TUVW 6
        D237 X -1
        Stop	
	Raise:
	    D237 WVUTSRQ 6
		Goto See
    }
}

ACTOR Torm_Hierophant2 : Torm_Hierophant1 1263
{
	//$Category "Tormentor Monsters"
	
	Tag "Hierophant"
	
	
    -FLOAT
    -NOGRAVITY
    +LOWGRAVITY
}

ACTOR Torm_HierophantTrailA
{
    RenderStyle "Translucent"
    Alpha 0.8
	
    +NOINTERACTION
    +CLIENTSIDEONLY
	
    States
    {
    Spawn:
        TNT1 A 3
        D237 A 5 A_FadeOut(0.10)
        Goto Spawn+1
    }
}

ACTOR Torm_HierophantTrailB : Torm_HierophantTrailA
{
    States
    {
    Spawn:
        TNT1 A 3
        D237 B 5 A_FadeOut(0.10)
        Goto Spawn+1
    }
}

ACTOR Torm_HierophantTrailC : Torm_HierophantTrailA
{
    States
    {
    Spawn:
        TNT1 A 3
        D237 C 5 A_FadeOut(0.10)
        Goto Spawn+1
    }
}

ACTOR Torm_HierophantTrailD : Torm_HierophantTrailA
{
    States
    {
    Spawn:
        TNT1 A 3
        D237 D 5 A_FadeOut(0.10)
        Goto Spawn+1
    }
}

ACTOR Torm_HierophantBall1 
{
	Projectile
	Speed 25
	FastSpeed 30
	Damage 5
	Radius 6
	Height 12
	SeeSound "Imp/Attack"
	DeathSound "Imp/ShotX"
    Decal "WizardScorch"
    Obituary "%o was personally carried to the holy land by the Hierophant."
	Renderstyle Add
	
	+FIREDAMAGE
	
    States
    {
    Spawn:
        D238 AB 4 Bright
        Loop
    Death:
        D238 C 0 Bright A_SetTranslucent(0.75, 1)
        D238 C 3 Bright
        D238 D 3 Bright 
        D238 EFG 3 Bright
        Stop
    }
}

ACTOR Torm_HierophantBall2 
{
    Radius 10
	Height 6
	Speed 15
	FastSpeed 22
    Damage 1
    Seesound "Torm_Hierophant/Fire"
    DeathSound "Torm_Hierophant/Hit"
    Decal "PlasmaScorchLower"
	Projectile
	RenderStyle Add
	
	+FIREDAMAGE
	+DONTREFLECT
    +SEEKERMISSILE
	
    States
    {
    Spawn:
        D239 ABCD 1 Bright A_Weave(3, 3, 1.0, 1.0)
        D239 A 0 A_SeekerMissile(5, 6)
        Loop
    Death:
        D239 E 0 A_SetTranslucent(0.75, 1)
        D239 EFGHIJ 3 Bright
        Stop
    }
}

ACTOR Torm_HierophantBall3 
{
	Radius 6
	Height 12
	Speed 15
	FastSpeed 21
    Damage 7
    Alpha 0.9
    SeeSound "Torm_Hierophant/Curse1"
    DeathSound "Torm_Hierophant/Curse2"
    Decal "MummyScorch"
	Renderstyle Add
    Projectile
	
    +HITMASTER
	+DONTREFLECT
    +SEEKERMISSILE
    +NODAMAGETHRUST
	
    States
    {
    Spawn:
        D240 A 2 Bright A_SpawnItemEx("Torm_HierophantBall3Trail", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
        D240 A 0 A_SeekerMissile(5,6)
        D240 B 2 Bright A_SpawnItemEx("Torm_HierophantBall3Trail", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
        D240 B 0 A_SeekerMissile(5,6)
        D240 C 2 Bright A_SpawnItemEx("Torm_HierophantBall3Trail", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
        D240 C 0 A_SeekerMissile(5,6)
        D240 D 2 Bright A_SpawnItemEx("Torm_HierophantBall3Trail", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE)
        D240 D 0 A_SeekerMissile(5,6)
        Loop
    Death:
        D240 E 0 Bright A_SetTranslucent(0.8, 1)
        D240 E 3 ACS_NamedExecuteWithResult("Torm_InflictCurse")
        D240 FGHI 3 Bright
        Stop
    }
}

ACTOR Torm_HierophantBall3Trail
{
    RenderStyle Add
    Alpha 0.8
	
    +NOINTERACTION
    +CLIENTSIDEONLY
	
    States
    {
    Spawn:
        TNT1 A 3
        D240 JKLMNOPQ 3 Bright
        Stop
    }
}

//===========================================================================
//
//  Powerups
//
//===========================================================================

ACTOR Torm_HumanCurse : PowerupGiver
{
    Inventory.MaxAmount 0
    Powerup.Duration -8
    Powerup.Color "Red" 0.15
    Powerup.Type "PowerTorm_HumanDMG"
	
    +Inventory.AutoActivate
    +Inventory.AlwaysPickup
}

ACTOR PowerTorm_HumanDMG : PowerProtection
{
    DamageFactor "Normal", 1.5
    DamageFactor "Fire", 1.5
    DamageFactor "Ice", 1.5
    DamageFactor "Melee", 1.5
    DamageFactor "Slime", 1.5
    DamageFactor "Railgun", 1.5
    DamageFactor "Poison", 1.5
    DamageFactor "PoisonCloud", 1.5
    DamageFactor "Electric", 1.5
	
    DamageFactor "Fire2", 1.5
}

ACTOR Torm_HumanCurse2 : PowerupGiver
{
    Inventory.MaxAmount 0
    Powerup.Duration -8
    Powerup.Color "Orange" 0.15
    Powerup.Type "PowerTorm_HumanSlow"
	
    +Inventory.AutoActivate
    +Inventory.AlwaysPickup
}

ACTOR PowerTorm_HumanSlow : PowerSpeed
{
    Speed 0.5
}