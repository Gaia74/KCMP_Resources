//===========================================================================
//
//  Slime Worm
//
//===========================================================================

ACTOR Torm_SlimeWorm 1245
{
	//$Category "Tormentor Monsters"
	
	Tag "Slime Worm"
	
	Health 150
	PainChance 140
	Speed 12
	Radius 24
	Height 56
	Mass 375
	Monster
    MeleeRange 60
	MaxTargetRange 375
    BloodColor "Yellow"
	SeeSound "Torm_SlimeWorm/Sight"
	AttackSound "Torm_SlimeWorm/Melee"
	PainSound "Torm_SlimeWorm/Pain"
	DeathSound "Torm_SlimeWorm/Death"
	ActiveSound "Torm_SlimeWorm/Active"
    HitObituary "%o was bitten by a Slime Worm."
    Species "Flesh"
	
	+FLOORCLIP
	+MISSILEMORE
	
    States
    {
    Spawn:
        D216 AB 10 A_Look
        Loop
    See:
		D216 ABCD 4 A_Chase
        Loop
	Melee:
        D216 EF 6 A_FaceTarget
        D216 G 6 A_CustomMeleeAttack(FRandom(1,8)*4,"Torm_SlimeWorm/Melee","Torm_SlimeWorm/Melee")
        Goto See
    Missile: 
        D216 E 0 A_JumpIfCloser(220,12)
        D216 E 0 A_JumpIfCloser(440,1)
	    Goto See
        D216 E 0
        D216 E 6 A_FaceTarget
        D216 F 6 A_PlaySound ("Torm_SlimeWorm/Vomit")
        D216 G 0 A_CustomMissile("Torm_SlimeWormBall1", 40, 0, random(-10,10), 2, random(10,20))
        D216 G 0 A_CustomMissile("Torm_SlimeWormBall2", 40, 0, random(-10,10), 2, random(10,20))
        D216 G 0 A_CustomMissile("Torm_SlimeWormBall3", 40, 0, random(-10,10), 2, random(10,20))
        D216 F 0 A_CustomMissile("Torm_SlimeWormBall4", 40, 0, random(-10,10), 2, random(10,20))
        D216 G 1 A_CustomMissile("Torm_SlimeWormBall5", 40, 0, random(-10,10), 2, random(10,20))
        D216 G 5
    	Goto See
        D216 E 0
        D216 E 6 A_FaceTarget
        D216 F 6 A_PlaySound ("Torm_SlimeWorm/Vomit")
        D216 G 0 A_CustomMissile("Torm_SlimeWormBall1", 40, 0, random(-10,10), 1, random(10,20))
        D216 G 0 A_CustomMissile("Torm_SlimeWormBall2", 40, 0, random(-10,10), 1, random(10,20))
        D216 G 0 A_CustomMissile("Torm_SlimeWormBall3", 40, 0, random(-10,10), 1, random(10,20))
        D216 F 0 A_CustomMissile("Torm_SlimeWormBall4", 40, 0, random(-10,10), 1, random(10,20))
        D216 G 1 A_CustomMissile("Torm_SlimeWormBall5", 40, 0, random(-10,10), 1, random(10,20))
        D216 G 5
    	Goto See
    Pain:
        D216 H 2
        D216 H 2 A_Pain
        Goto See
    Death:
        D216 I 4 A_Scream
		D216 J 4 
        D216 JK 4
        D216 L 4 A_NoBlocking
        D216 M 4 
		D216 N -1
        Stop 
    Raise:
        D216 NMLKJI 4
        Goto See
    }
}

ACTOR Torm_SlimeWormBall1 
{
    Radius 4
    Height 4
	Speed 18
	FastSpeed 21
    Scale 0.52
    Damage 1
    Gravity 0.85
	Projectile
    DeathSound "Torm_SlimeWorm/Hit"
    Obituary "%o's flesh was dissolved off by a Slime Worm."
    Decal "PlasmaScorchLower"
	PoisonDamage 23
	RenderStyle Add
	
	+RANDOMIZE
    -NOGRAVITY
	
    States
    {
    Spawn:
        D217 A 0 
        D217 A 0 A_Jump(256,1,2,3)
	Spawn2:
        D217 ABC 2
        Loop
    Death:
        D217 D 0 A_NoGravity
        D217 DEF 4
        Stop
    }
}

ACTOR Torm_SlimeWormBall2 : Torm_SlimeWormBall1
{
    Speed 20
    Scale 0.54
}

ACTOR Torm_SlimeWormBall3 : Torm_SlimeWormBall1
{
    Speed 22
    Scale 0.56
}

ACTOR Torm_SlimeWormBall4 : Torm_SlimeWormBall1
{
    Speed 24
    Scale 0.58
}

ACTOR Torm_SlimeWormBall5 : Torm_SlimeWormBall1
{
    Speed 26
    Scale 0.60
}