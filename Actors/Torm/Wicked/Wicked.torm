//===========================================================================
//
//  Wicked
//
//===========================================================================

ACTOR Torm_Wicked 1250
{
	//$Category "Tormentor Monsters"
	
	Tag "Wicked"
	
    Health 300
	Painchance 64
	Speed 15
    Radius 25
    Height 85
	Mass 825
	SeeSound "Torm_Wicked/Sight"
    ActiveSound "Torm_Wicked/Active"
    PainSound "Torm_Wicked/Pain"
    DeathSound "Torm_Wicked/Death"
    Obituary "%o got blasted by a Wicked."
	Species "Flesh"
    Monster  
	
	+FLOAT
	+NOGRAVITY 
	+FIRERESIST
	+MISSILEMORE
    +QUICKTORETALIATE
	
    States
    {
    Spawn: 
		D225 AABBCCDD 4 A_Look
        Loop
	See:
	    D225 AABBCCDD 4 A_Chase
        D225 A 0 A_Jump(64,1)
        Goto See
        D225 A 0
        Goto See2
    See2:
        D225 EEFFGGHH 2 A_Chase
        D225 E 0 A_Jump(32,1)
        Goto See2
        D225 E 0
        Goto See
    Missile:
        D225 I 0 
        D225 I 5 A_FaceTarget
        D225 J 5 Bright A_FaceTarget  
        D225 K 8 Bright A_CustomMissile("Torm_WickedBall", 60, 0, 0)
        D225 L 5
        D225 A 0 A_Jump(64,1)
        Goto See2
	    D225 A 0
	    Goto See
	Pain: 
        D225 M 3 
        D225 M 3 A_Pain 
        D225 M 3 
        Goto See 
	Death: 
        D225 N 4 A_Scream
        D225 O 4
		D225 P 4 A_NoBlocking
        D225 QRSTUV 4 
		D225 W -1 A_SetFloorClip 
        Stop 
	Raise:
        D225 T 4 A_UnSetFloorClip 
        D225 SRQPON 4
        Goto See 
    }
}

ACTOR Torm_WickedBall
{
    Radius 8
    Height 8
    Speed 20
	FastSpeed 25
    Damage 5
    Alpha 0.67
    RenderStyle Add
    Projectile
    SeeSound "Torm_Wicked/Fire"
    DeathSound "Torm_Wicked/Hit"
    Decal "DoomImpScorch"
	
    +SEEKERMISSILE
	+FIREDAMAGE
	
    States
    {
	Spawn:
        D226 A 0
	    D226 A 0 A_SeekerMissile(8,8, SMF_PRECISE)
        D226 ABCD 1 Bright A_SpawnItemEx("Torm_WickedBallDX", 0, 0, 0, FRandom(-1,1), FRandom(-1,1), FRandom(-1,1), 0, 128, 0)
	    D226 ABCD 1 Bright A_SpawnItemEx("Torm_WickedBallDX", 0, 0, 0, FRandom(-1,1), FRandom(-1,1), FRandom(-1,1), 0, 128, 0)
        Loop
    Death:
        D226 JKLMN 3 Bright
        Stop
    }
}

ACTOR Torm_WickedBallDX
{   
    Radius 2
    Height 2
    Alpha 0.67
    RenderStyle Add
	
    +NOGRAVITY
    +NOBLOCKMAP
    +DONTSPLASH
	
    States
    {
    Spawn:
        TNT1 A 3 
        D226 EFGHI 3 Bright
        Stop 
    }
}