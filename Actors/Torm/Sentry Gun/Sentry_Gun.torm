//===========================================================================
//
//  Sentry Gun
//
//===========================================================================

ACTOR Torm_SentryGun 1246
{
	//$Category "Tormentor Monsters"
	
	Tag "Sentry Gun"
	
    Health 80
	Painchance 72
	Speed 0
    Radius 20
    Height 56
	Mass 1000
    SeeSound "Torm_SentryGun/Sight"
    ActiveSound "Torm_SentryGun/Active"
    PainSound "Torm_SentryGun/Pain"
    DeathSound "Torm_SentryGun/Death"
    Obituary "%o was eradicated by a Fire Sentry gun."
	Species "Tech"
	Monster
	
	+NOBLOOD
    +FLOORCLIP
	+FIRERESIST
	+NOICEDEATH
    +STANDSTILL
    +MISSILEMORE
    +NOBLOODDECALS
    +LOOKALLAROUND
    +QUICKTORETALIATE
	
    States
    {
    Spawn:
        D218 A 2 A_Look
        Loop
    See:
        D218 A 2 A_Chase
        Loop
	Pain:
	    D218 A 10 A_Pain
		Goto See
	 Missile: 
		D218 A 0 A_PlaySound("Torm_SentryGun/Alert")
		D218 A 20 A_FaceTarget
		D218 A 0 A_Jumpifcloser(200, "Missile2")
		D218 A 3 A_FaceTarget
		D218 B 0 A_PlaySound("Torm_SentryGun/Shot")
		D218 B 1 Bright A_CustomMissile("Torm_SentryGunHitcan1",42,0,FRandom(-9,9))
		D218 B 2 Bright
		D218 A 0 A_MonsterRefire(50, "See")
        Goto Missile+2
    Missile2: 
		D218 A 3 A_FaceTarget
		D218 B 0 A_PlaySound("Torm_SentryGun/Shot")
		D218 B 1 Bright A_CustomMissile("Torm_SentryGunHitcan2",42,0,FRandom(-9,9))
		D218 B 2 Bright
		D218 A 0 A_MonsterRefire(50, "See")
        Goto Missile+2
    Death:
        D218 A 1 A_Scream
		D218 C 0 A_NoBlocking
        D218 C 0 A_PlaySoundEx("Weapons/Rocklx",CHAN_ITEM)
        D218 C 0 A_SpawnItem ("Torm_SentryGunBoom", 0, 42)
		D218 CC 0 A_SpawnItemEx("Torm_MetalRandom2", 0, 0, FRandom(42,48), 0, 0, 0, 0, 128)
		D218 C 0 A_SpawnItemEx("Torm_MetalRandom2", 0,0, FRandom(42,48), random(2,6),random(2,6),random(2,6), random(0,360), 128)
	    D218 C 0 A_SpawnItemEx("Torm_MetalRandom2", 0,0, FRandom(42,48), random(2,6),random(2,6),random(2,6), random(0,360), 128)
        D218 C 1
        D218 C -1
        Stop
    }
}

ACTOR Torm_SentryGunBoom
{
    Radius 2
    Height 2
    RenderStyle Add
	
    +NOINTERACTION
    +CLIENTSIDEONLY
	
    States
    {
    Spawn:
        D218 DEFGHI 3 Bright
        Stop
    }
}

ACTOR Torm_SentryGunHitcan1 
{
    Damage (random(1,4))
    Radius 7
    Height 7
    Speed 150
    Projectile
    Decal "Bulletchip"
	
	+NOGRAVITY
	+DONTBLAST
	+NOBLOCKMAP
    +DONTREFLECT
    +BLOODSPLATTER
	
    States
    {
    Spawn:
        TNT1 A 1
        Loop
	Crash:
	Death:
        TNT1 A 1 A_SpawnItemEx("BulletPuff", 0, 0, 0, 0, 0, 1, 0, SXF_NOCHECKPOSITION)
        Stop
	XDeath:
	    TNT1 A 1
        Stop
    }
}

ACTOR Torm_SentryGunHitcan2 : Torm_SentryGunHitcan1
{
    Speed 35
}