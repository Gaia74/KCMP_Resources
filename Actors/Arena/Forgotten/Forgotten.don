//===========================================================================
//
//  Forgotten
//
//===========================================================================

ACTOR Don_Forgotten 1212
{
	//$Category "Don Monsters"
	
	Tag "Forgotten"
	
	Health 100
	Radius 16
	Height 56
	Mass 100
	Speed 7
	PainChance 224
	Monster
	SeeSound "Don_Forgotten/Sight"
	ActiveSound "Don_Forgotten/Active"
	PainSound "Don_Forgotten/Pain"
	DeathSound "Don_Forgotten/Death"
	RenderStyle SoulTrans
	Species "Soul"
	
	+FLOAT 
	+DONTFALL 
	+NOGRAVITY 
	+MISSILEMORE
	+NOBLOODDECALS
	+DONTHARMSPECIES
	
	States
	{
	Spawn:
		D012 AB 10 Bright A_LookEx(0, 0, 0, 0, 0, "See2")
		Loop
	See:
	    D012 D 1 Bright A_CustomMissile("Don_ForgottenMissile", 32, 0, 0, 1)
		Goto See2
	See2:
		D012 AB 5 Bright A_Chase
        Loop	
    Missile:
	    D012 C 0 A_PlaySound("Don_Forgotten/Attack",CHAN_WEAPON)
        D012 C 4 Bright A_FaceTarget
        D012 D 4 Bright A_SkullAttack(30)
        D012 CD 4 Bright
        Goto Missile+3	
	Pain:
		D012 E 3 Bright
		D012 E 3 Bright A_Pain
		Goto See2	
	Death:
	    D012 F 2 Bright A_Scream
        D012 GH 2 Bright 
		D012 IJ 3 Bright  
		D012 K 4 Bright A_PlaySound("Weapons/RockLX",CHAN_WEAPON)
		D012 L 0 A_Stop
		D012 L 0 A_SpawnItemEx("Don_ForgottenWave", 0, 0, 20, 0, 0, 0, 0, 128)
		D012 L 0 A_CustomMissile("Don_ForgottenSplode", 32, 0, 0, 1)
		D012 L 4 Bright A_NoBlocking
		D012 MN 4 Bright 	
        Stop
	}
}

ACTOR Don_ForgottenWave
{
    Radius 1
	Height 1
	Speed 0
	Alpha 0.5
	RenderStyle Add
	
	+NOBLOCKMAP
	+NOINTERACTION
	+CLIENTSIDEONLY
	
	States
	{
	Spawn:
	    D039 ABCDEFGHIJKLMNOPQR 1 Bright A_FadeOut(0.038)
		Stop
	}
}


ACTOR Don_ForgottenMissile
{
    Radius 10
    Height 10
    Speed 20
    Health 1
    Damage 4
	Obituary "%o was caught by a Forgotten Spirit." 
	
    +MISSILE
    +DROPOFF
    +NOGRAVITY
    +NOTELEPORT	
	+DONTREFLECT
	+NOEXTREMEDEATH
    +ACTIVATEIMPACT
    +ACTIVATEPCROSS
	
    States
    {
    Spawn:
        TNT1 A 4
    Death:
        TNT1 A 1
        Stop
    }
}

ACTOR Don_ForgottenSplode
{
    Speed 1
    Projectile
    Height 5
    Radius 5
    Obituary "%o couldn't hide from a Forgotten Spirit."
	
    +RANDOMIZE
    +DONTREFLECT
	+FORCERADIUSDMG
	
    States
    {
    Spawn:
        TNT1 A 1
    Death:
        TNT1 A 1 A_Explode(64, 128, 0)
        Stop
    }
}