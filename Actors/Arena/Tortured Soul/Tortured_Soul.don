//===========================================================================
//
//  Tortured Soul
//
//===========================================================================

ACTOR Don_TorturedSoul 1216
{
	//$Category "Don Monsters"
	
	Tag "Tortured Soul"
	
	Health 150
    Mass 350
    Speed 6
    Radius 28
	Height 56
	PainChance 100
	Monster
	Alpha 0.4
    BloodColor "0 78 0"
	SeeSound "Don_TorturedSoul/Sight"
	ActiveSound "Don_TorturedSoul/Active"
	PainSound "Don_TorturedSoul/Pain"
	DeathSound "Don_TorturedSoul/Death"
    Obituary "%o was poisoned by a Tortured Soul."
	RenderStyle Translucent
	Species "Tortured"
	
	+GHOST
	+FLOAT 
    +SHADOW
	+NOGRAVITY 
    +QUICKTORETALIATE
	
	States
	{
	Spawn:
		D016 A 10 A_Look
		Loop
	See:
		D016 AABBCCBBAA 3 A_Chase
		Loop
    Missile:
        D016 D 0 A_Jump(40,"Seeker")
	    D016 D 7 A_FaceTarget
		D016 E 6 Bright A_FaceTarget
	    D016 F 0 A_FaceTarget
     	D016 F 5 Bright A_CustomMissile("Don_TorturedSoulBall1", 40, 0, 0)
	 	Goto See
	Seeker:
	    D016 D 7 A_FaceTarget
		D016 E 6 Bright A_FaceTarget
	    D016 F 0 A_FaceTarget
     	D016 F 5 Bright A_CustomMissile("Don_TorturedSoulBall2", 40, 0, 0)
	 	Goto See
	Pain:
		D016 G 3
		D016 G 3 A_Pain
		D016 G 3
		Goto See	
	Death:
      	D016 H 0 A_Jump(128,8) 
      	D016 H 8 
      	D016 I 8 A_Scream
		D016 J 7 Bright 
		D016 K 6 Bright A_NoBlocking
		D016 LM 6 Bright
      	D016 N 5 Bright
        Stop
      	D016 H 8 
      	D016 I 8 A_Scream
		D016 J 7 Bright 
		D016 K 6 Bright A_NoBlocking
		D016 L 0 A_PlaySound("Don_TorturedSoul/Explode",CHAN_WEAPON)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(-3,3), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(42,48), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(87,93), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(132,138), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(177,183), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(232,238), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(267,273), 2)
		D016 L 0 A_CustomMissile("Don_TorturedSoulPoison", 30, 0, FRandom(312,318), 2) 
		D016 LM 6 Bright
      	D016 N 5 Bright
        Stop
	}
}

ACTOR Don_TorturedSoulBall1
{  
	Height 6
	Radius 6
	Speed 20
	FastSpeed 28
	Damage ((2*FRandom(7,13)))
	Renderstyle Add
	PoisonDamage 25
	Alpha 0.8
	SeeSound "Don_TorturedSoul/Fire"
	DeathSound "Don_TorturedSoul/Hit"
	Decal "DoomImpScorch"
	Projectile
	
	States
	{
    Spawn:
		D043 ABC 3 Bright A_Weave(3,0,2,0)
		Loop
	Death:
		D043 DEFG 4 Bright
	    Stop
    }
}

ACTOR Don_TorturedSoulBall2 : Don_TorturedSoulBall1
{  
	Speed 22
	FastSpeed 30
	Damage ((2*FRandom(9,15)))
	
    +SEEKERMISSILE
	
	States
	{
    Spawn:
		D043 A 3 Bright A_SpawnItemEx("Don_TorturedSoulBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
        D043 A 0 A_BishopMissileWeave
		D043 B 3 Bright A_SpawnItemEx("Don_TorturedSoulBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
	    D043 B 0 A_BishopMissileWeave
		D043 C 3 Bright A_SpawnItemEx("Don_TorturedSoulBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
	    D043 C 0 A_BishopMissileWeave
		D043 A 0 A_SeekerMissile(7, 14)
		Loop
    }
}

ACTOR Don_TorturedSoulBallTrail
{
    Radius 0
    Height 1
    Speed 0
    Projectile
	Scale 0.62
    RenderStyle Add
	
    States
    {
    Spawn:
	    TNT1 A 1
        D044 ABCDEFHIJ 3 Bright
        Stop
    }
}

ACTOR Don_TorturedSoulPoison
{
    Speed 4
    FastSpeed 7
	Height 28
	Radius 20
	Damage 4
	ReactionTime 6
	Alpha 0.75
	PoisonDamage 64
	Obituary "%o was poisoned by a Tortured Soul."
	Renderstyle Add
	Projectile
	
	+FLOATBOB
	+NOGRAVITY
	+DONTREFLECT
	
	States
	{
	Spawn:
		D045 CD 4
	Cloud:
		D045 EFG 4
		D045 E 0 A_CountDown
		Loop
	Death:
	    D045 D 4
	    D045 C 4 A_FadeOut(0.24)
	    Wait
	}
}