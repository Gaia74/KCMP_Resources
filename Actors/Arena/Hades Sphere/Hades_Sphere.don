//===========================================================================
//
//  Hades Sphere
//
//===========================================================================

ACTOR Don_HadesSphere 
{
	//$Category "Don Monsters"
	
	Tag "Hades Sphere"
	
	Health 50
    Mass 0x7fffffff
    Speed 11
    Radius 16
    Height 42
	PainChance 0
	Scale 0.85
	MeleeRange 54
	Monster
	SeeSound "Don_HadesSphere/Sight"
	DeathSound "Don_HadesSphere/Death"
    Obituary "%o was blown away by a Hades Sphere."
	Species "Hades"
	
    +FLOAT
	+NOBLOOD
	+FLOATBOB
    +DONTFALL 
    +NOGRAVITY
	+NOICEDEATH
	+DONTHARMSPECIES
    +QUICKTORETALIATE
	
	States
	{
	Spawn:
	    TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Don_HadesSphereCheck", 1, "Spawn2")
		TNT1 A 1 A_GiveInventory("Don_HadesSphereCheck",1)
	    TNT1 A 0 A_Stop
		TNT1 A 2 A_Look
		Goto Spawn+4
	Spawn2:
        D018 A 0 A_Playsound("Don_HadesSphere/Active",CHAN_BODY)
	    D018 AABBCCDDCCBB 2 Bright A_LookEx(0, 0, 0, 0, 0, "SeeLoop")
		Loop
	See:
        D018 EFGH 2 Bright
    SeeLoop:
        D018 A 0 A_Playsound("Don_HadesSphere/Active",CHAN_BODY)
        D018 AABBCCDDCCBB 2 Bright A_Chase
        D018 A 0 A_Jump(56, "TeleportOut")
        Loop
    TeleportOut:
        D018 H 0 A_Stop
        D018 H 0 A_UnSetSolid
        D018 H 0 A_UnSetShootable
        D018 H 3 Bright A_Playsound("Don_HadesSphere/Teleport",CHAN_WEAPON)
        D018 GFEIJKL 3 Bright
		TNT1 A 1 A_SetTranslucent(0.0)
    SeeTeleport:
        TNT1 AAAAAAAA 2 A_Chase("","")
        TNT1 A 0 A_Jump(96, "TeleportIn")
        Loop
    TeleportIn:
		TNT1 A 1 A_SetTranslucent(1.0)
        D018 L 3 Bright A_Playsound("Don_HadesSphere/Teleport",CHAN_WEAPON)
        D018 KJIEFG 3 Bright
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_SetSolid
        Goto SeeLoop
    Melee:
        TNT1 A 0 A_SetInvulnerable
        D018 HGFE 2 Bright
        TNT1 A 0 A_UnSetInvulnerable
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_Die("DieMelee")
        Stop
    Death.DieMelee:
        TNT1 A 0 A_NoBlocking
	    TNT1 A 0 A_SetTranslucent(0.9, 1)
		TNT1 A 0 A_SetScale(0.78,0.78)
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_Nogravity
		TNT1 A 0 A_Stop
	    TNT1 A 0 A_PlaySound("Don_HadesSphere/Explode",CHAN_BODY)
		TNT1 A 0 A_SpawnItemEx("Don_ForgottenWave", 0, 0, 20, 0, 0, 0, 0, 128)
	    D018 M 3 Bright A_Custommissile("Don_HadesSphereExplode",28,0)
        D018 NOPQ 3 Bright
        Stop
    Death:
        TNT1 A 0 A_Noblocking
	    TNT1 A 0 A_SetTranslucent(1, 1)
		TNT1 A 0 A_Nogravity
		TNT1 A 0 A_Stop
        D018 H 3 Bright A_Scream
        D018 GFE 3 Bright
        D018 RSTUVWX 3 Bright
        Stop
	}
}

ACTOR Don_HadesSphere2 : Don_HadesSphere 1217
{
	//$Category "Don Monsters"
	
	Tag "Hades Sphere"
	
    States
    {
    Spawn:
        D018 AABBCCDDCCBB 2 Bright A_Look
        Loop
    See:
        D018 A 0 A_Playsound("Don_HadesSphere/Active",CHAN_BODY)
        D018 AABBCCDDCCBB 2 Bright A_Chase
        D018 A 0 A_Jump(56, "TeleportOut")
        Loop
    TeleportIn:
        TNT1 A 1 A_SetTranslucent(1.0)
        D018 L 3 Bright A_Playsound("Don_HadesSphere/Teleport",CHAN_WEAPON)
        D018 KJIEFG 3 Bright
        TNT1 A 0 A_SetShootable
        TNT1 A 0 A_SetSolid
        Goto See
    }
}

ACTOR Don_HadesSphereExplode
{
    Speed 1
    Projectile
    Height 5
    Radius 5
	
    +RANDOMIZE
    +DONTREFLECT
	+EXTREMEDEATH
	+FORCERADIUSDMG
	
    States
    {
    Spawn:
        TNT1 A 1
    Death:
	    TNT1 A 0
		TNT1 AAAA 0 A_SpawnItemEx("Don_HadesSphereEffect", 0, 0, 2, (0.1)*Random(-20, 20), (0.1)*Random(-20, 20), (0.1)*Random(-20, 20), Random(0, 360), 128)
        TNT1 A 1 A_Explode(128, 156, 0)
        Stop
    }
}

ACTOR Don_HadesSphereEffect
{
	Scale 0.65
    RenderStyle Add
	
    +MISSILE
    +NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	
    States
    {
    Spawn:
        D048 ABCDEFGHIJ  1 Bright 
		D048 KK 1 Bright A_SetScale(0.60,0.60)
		D048 LL 1 Bright A_SetScale(0.54,0.54)
		D048 MM 1 Bright A_SetScale(0.47,0.47)
		D048 NN 1 Bright A_SetScale(0.39,0.39)
		D048 OO 1 Bright A_SetScale(0.30,0.30)
		D048 PP 1 Bright A_SetScale(0.20,0.20) 
		D048 QQ 1 Bright A_SetScale(0.10,0.10) 
        Stop
    }
}

ACTOR Don_HadesSphereCheck : Inventory
{
    Inventory.Maxamount 1
}
