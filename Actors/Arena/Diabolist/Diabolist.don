//===========================================================================
//
//  Diabolist
//
//===========================================================================

ACTOR Don_Diabolist 1224
{
	//$Category "Don Monsters"
	
	Tag "Diabolist"
	
	Health 700
	Radius 20
	Height 56
	Mass 500
	Speed 15
	PainChance 10
	Monster
	BloodColor "08 08 08"
	SeeSound "Don_Diabolist/Sight"
	PainSound "Don_Diabolist/Pain"
	DeathSound "Don_Diabolist/Death"
	ActiveSound "Don_Diabolist/Active"
	Obituary "%o was set ablaze by a Diabolist."
	Species "Diablo"
	
	+FLOORCLIP 
	+FIRERESIST
	+DONTHARMSPECIES
	+QUICKTORETALIATE 
	var int user_nohitscanattack;

	States
	{
	Spawn:
		D024 AB 10 A_Look
		Loop
	See:		
		D024 AABBCC 2 A_Chase
		D024 D 0 //A_PlaySound("Don_Soldier/Step")
		D024 DDEEFF 2 A_Chase
		D024 A 0 //A_PlaySound("Don_Soldier/Step")
		Loop
	Missile:
        D024 A 0 A_JumpIfCloser(280,"FireSpread")
		D024 A 0 A_Jump(64,4)
		D024 A 0 A_Jump(256,6)
		Goto See
		D024 A 0 A_JumpIf(user_nohitscanattack,4)
		D024 A 0 A_JumpIfCloser(1024,"Burning")
		D024 A 0 A_Jump(256,2)
		Goto See
		D024 "[" 0
		D024 "[" 0 A_Jump(96,"MissileFlame")
		D024 "[" 8 Bright A_FaceTarget
        D024 "\" 8 Bright A_FaceTarget
        D024 "\" 0 A_FaceTarget
        D024 "\" 0 A_CustomMissile("Don_DiabolistTracer",0,-32,0)
        D024 "]" 8 Bright A_CustomMissile("Don_DiabolistTracer",0,32,0)
        Goto See
	Burning:
		D024 G 0 A_VileStart
		D024 G 10 Bright A_VileTarget("Don_DiabolistFire") 
		D024 H 12 Bright A_FaceTarget// A_VileTarget("Don_DiabolistFire")
		D024 IJKLMN 8 Bright A_FaceTarget 
		D024 O 8 Bright A_VileAttack("Vile/Stop",20,70,70,1.0,"Fire",VAF_DMGTYPEAPPLYTODIRECT)
		D024 P 20 Bright
        Goto See
	FireSpread:
		D024 G 0 A_PlaySound("Vile/Start",CHAN_WEAPON)
		D024 G 4 Bright A_FaceTarget
		D024 HIJKLMN 4 Bright A_FaceTarget
		D024 N 0 A_FaceTarget
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,0)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,6)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,12)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,18)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,24)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,30)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,-30)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,-24)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,-18)
		D024 N 0 A_CustomMissile("Don_DiabolistSpread",26,0,-12)
		D024 O 5 Bright A_CustomMissile("Don_DiabolistSpread",26,0,-6)
		D024 P 15 Bright A_FaceTarget
		Goto See
	MissileFlame:
	    D024 G 8 Bright A_FaceTarget
		D024 G 0 A_PlaySound("Vile/Start",CHAN_WEAPON)
        D024 H 6 Bright A_FaceTarget
        D024 IJKLMN 6 Bright A_FaceTarget
		D024 O 0 A_FaceTarget
		D024 O 0 A_PlaySound("Don_Diabolist/Fire2",CHAN_WEAPON)
		D024 O 5 Bright A_CustomMissile("Don_DiabolistFlame",32,0,0)
		D024 P 15 Bright A_FaceTarget
		Goto See
	Pain:
		D024 Q 5   
		D024 Q 5 A_Pain
		Goto See
	Death:
		D024 Q 7
		D024 R 7 A_Scream
		D024 S 7 A_NoBlocking
		D024 T 7 A_SpawnItemEx("Don_DiabolistDeathFire", 0, 0, 12, 0, 0, 0, 0)
		D024 U 7 A_PlaySound("Don_Body/Fall",CHAN_BODY,0.5)
		D024 VWXY 7
		D024 Z -1
		Stop
	}
}

ACTOR Don_DiabolistTracer
{
    Radius 6
    Height 6
    Speed 15
    Damage 4
	Scale 1.35
    Projectile
	DamageType "Fire"
    SeeSound "Don_Diabolist/Fire1"
    DeathSound "Don_Diabolist/Hit1"
    RenderStyle Add
	Decal "Scorch"
	
    +FLOORHUGGER
	
    States
    {
    Spawn:
        TNT1 A 1 A_CStaffMissileSlither
        TNT1 A 0 A_SpawnItem("Don_DiabolistTracerTrail", 0, 0)
        Loop
    Death:
        D054 K 4 Bright A_Explode(32, 100, 0)
        D054 L 4 Bright 
        D054 MNO 3 Bright
        Stop
    }
}

ACTOR Don_DiabolistTracerTrail
{
    Radius 1
    Height 1
    Speed 0
    RenderStyle Add
    Projectile
	
    +FLOORHUGGER
	+NOGRAVITY
	
    States
    {
    Spawn:
		D054 CDEFBIAJ 3 Bright
        Stop
    }
}

ACTOR Don_DiabolistSpread
{
	Speed 12
	Radius 16
	Height 26
	Alpha 0.75
	Damage 2
	Projectile
	Decal "Scorch"
	DamageType "Fire"
    DeathSound "Don_Diabolist/Hit2"
	RenderStyle Add
	
	+DONTREFLECT
	
	States
	{
	Spawn:
		D053 AB 2 Bright
		D053 A 2 Bright A_FireCrackle
		D053 BCBCDEDEFEFGFGH 2 Bright
		Stop
	Death:
		D053 A 4 Bright A_PlaySound("Diabolist/SpawnFire",0)
		D053 BCDEFGH 4 Bright
		Stop
	}
}

ACTOR Don_DiabolistFire
{
    Radius 0
    Height 1
    Speed 0
	DamageType "Fire"
    RenderStyle Add
	
    +NOGRAVITY
	+DONTREFLECT
    +SEEKERMISSILE
	+FORCERADIUSDMG
	
	States
	{
	Spawn:
		TNT1 AA 0 Bright A_StartFire
		D056 MMMNNOOOPP 1 Bright A_Warp(AAPTR_TRACER,20,0,20,0,WARPF_NOCHECKPOSITION)
		TNT1 AAAAA 3 A_Warp(AAPTR_TRACER,20,0,0,0,WARPF_NOCHECKPOSITION)
		D053 A 2 Bright A_StartFire
        D053 A 0 A_Explode(3, 32)
        D053 B 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 A 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 B 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 C 2 Bright A_FireCrackle
        D053 A 0 A_Explode(4, 32)
        D053 B 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 C 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 B 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 C 2 Bright A_Fire
        D053 A 0 A_Explode(5, 32)
        D053 D 2 Bright A_Fire
        D053 A 0 A_Explode(5, 32)
        D053 C 2 Bright A_Fire
        D053 A 0 A_Explode(5, 32)
        D053 D 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 C 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 D 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 E 2 Bright A_Fire
        D053 A 0 A_Explode(4, 32)
        D053 D 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 E 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 D 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 E 2 Bright A_FireCrackle
        D053 A 0 A_Explode(3, 32)
        D053 F 2 Bright A_Fire
        D053 A 0 A_Explode(3, 32)
        D053 E 2 Bright A_Fire
        D053 A 0 A_Explode(2, 32)
        D053 F 2 Bright A_Fire
        D053 A 0 A_Explode(2, 32)
        D053 E 2 Bright A_Fire
        D053 A 0 A_Explode(2, 32)
        D053 F 2 Bright A_Fire
        D053 A 0 A_Explode(2, 32)
        D053 G 2 Bright A_Fire
        D053 A 0 A_Explode(2, 32)
        D053 H 2 Bright A_Fire
        D053 A 0 A_Explode(1, 32)
        D053 G 2 Bright A_Fire
        D053 A 0 A_Explode(1, 32)
        D053 H 2 Bright A_Fire
        D053 A 0 A_Explode(1, 32)
        D053 G 2 Bright A_Fire
        D053 A 0 A_Explode(1, 32)
        D053 H 2 Bright A_Fire
        Stop
	}
}

ACTOR Don_DiabolistDeathFire
{
	Height 46
	Radius 20
    Projectile
	Damage 1
	Speed 0
	DamageType "Fire"
	Renderstyle Add
	
	+RIPPER
	+DONTREFLECT
	+BLOODLESSIMPACT
	
	States
	{
	Spawn:
		D055 A 2 Bright A_StartFire
		D055 B 2 Bright
		D055 CDEFGHI 2 Bright
		D055 I 0 A_FireCrackle
		D055 CDEFGHI 2 Bright
		D055 I 0 A_FireCrackle
		D055 CDEFGHI 2 Bright
		D055 I 0 A_FireCrackle
		D055 CDEFGHI 2 Bright
		D055 I 0 A_FireCrackle
		D055 CDEFGHI 2 Bright
		D055 I 0 A_FireCrackle
		D055 JKLM 3 Bright
		Stop
	}
}

ACTOR Don_DiabolistFlame : FastProjectile
{
	Radius 14
	Height 8
	Speed 65
	DamageType "Fire"
	MissileType "Don_DiabolistFlameTrailSpawner"
	Damage 7
	SeeSound "Don_Diabolist/Fire2"
	DeathSound "Don_Diabolist/Hit3"
	Decal "WizardScorch"
	RenderStyle Add
	
	-INVISIBLE
	+DONTREFLECT
	
	States
	{
	Spawn:
		TNT1 A -1
		Loop
	Crash:
	XDeath:
		D055 A 1 Bright 
		D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 0, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 45, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 90, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 135, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 180, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 225, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 270, 28, 0)
        D055 A 0 A_SpawnItemEx("Don_DiabolistCircleFlame",40, 0, -10, 0, 0, 0, 315, 28, 0)
		D055 ABC 3 Bright
		D055 D 4 Bright
		D055 E 3 Bright
		D055 F 4 Bright
		D055 G 3 Bright
		D055 H 4 Bright
		D055 I 3 Bright
		D055 J 4 Bright
		D055 K 3 Bright
		D055 L 4 Bright
		D055 M 3 Bright
		Stop
	Death:
		D055 A 1 Bright 
		D055 ABC 3 Bright
		D055 D 4 Bright
		D055 E 3 Bright
		D055 F 4 Bright
		D055 G 3 Bright
		D055 H 4 Bright
		D055 I 3 Bright
		D055 J 4 Bright
		D055 K 3 Bright
		D055 L 4 Bright
		D055 M 3 Bright
		Stop
	}
}

ACTOR Don_DiabolistFlameTrailSpawner
{
	+NOBLOCKMAP
	+NOGRAVITY
	
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_SpawnItemEx("Don_DiabolistFlameTrail", 0, 0, 8, 0, 0, 0, 0, SXF_CLIENTSIDE, 0)
		Stop
	}
}

ACTOR Don_DiabolistFlameTrail : CFlameFloor 
{
    Alpha 0.9
	States
	{
	Spawn:
		D055 NNOOPPPPPPPPPPPPPPPPPPPP 1 Bright A_FadeOut(0.15)
		Stop
	}
}

ACTOR Don_DiabolistCircleFlame : CircleFlame 
{
	Damage 3
	DamageType "Fire"
	DeathSound "Don_Diabolist/Hit4"
	
	+DONTREFLECT
	
	States
	{
	Spawn:
		D056 A 4 Bright
		D056 B 2 Bright A_CFlameRotate
		D056 C 2 Bright
		D056 D 1 Bright
		D056 E 2 Bright
		D056 F 2 Bright A_CFlameRotate
		D056 G 1 Bright
		D056 HI 2 Bright
		D056 J 1 Bright A_CFlameRotate
		D056 K 2 Bright
		D056 LM 3 Bright
		D056 N 2 Bright A_CFlameRotate
		D056 O 3 Bright
		D056 P 2 Bright
		Stop
	Death:
		D056 QR 3 Bright
		D056 S 3 Bright A_Explode(20, 32, 0)
		D056 TUVWXYZ 3 Bright
		Stop
	}
}
