//===========================================================================
//
//  Hades Elemental
//
//===========================================================================

ACTOR Don_HadesElemental 1218
{
	//$Category "Don Monsters"
	
	Tag "Hades Elemental"
	
	Health 400
    Mass 425
    Speed 7
    Height 60
    Radius 28
	PainChance 112
	Scale 0.86
	MeleeRange 50
	Monster
	SeeSound "Don_HadesElemental/Sight"
	ActiveSound "Don_HadesElemental/Active"
	PainSound "Don_HadesElemental/Pain"
	DeathSound "Don_HadesElemental/Death"
    HitObituary "%o got chomped by a Hades Elemental."
	Species "Hades"
	
	+FLOAT 
	+NOGRAVITY 
	+MISSILEMORE
    +QUICKTORETALIATE
	
	States
	{
	Spawn:
		D017 A 10 A_Look
		Loop
	See:
      	D017 AAAAAAAAAAAA 3 A_Chase 	
		D017 A 0 A_Jump(96,"Teleport")
		D017 AAAAAAAAAAAA 3 A_Chase 
		Loop
	Melee:
	    D017 BC 6 A_Facetarget
     	D017 D 7 A_CustomMeleeAttack(FRandom(1,6)*10, "Don_HadesElemental/Melee", "Don_HadesElemental/Melee")
	 	Goto See
	Missile:
     	D017 EF 6 Bright A_Facetarget
		D017 F 0 A_PlaySound("Don_HadesElemental/Fire",CHAN_WEAPON)
		D017 F 0 A_CustomMissile("Don_HadesElementalBall",25,0,-5,0,0)
		D017 F 0 A_CustomMissile("Don_HadesElementalBall",25,0,5,0,0)
     	D017 G 7 Bright A_CustomMissile("Don_HadesElementalBall",25,0,0,0,0)
	 	Goto See	
	Teleport:
	    D017 A 0 A_Stop
	    D017 A 0 A_ChangeFlag(NOPAIN,1) 
	    D017 A 0 A_UnSetshootable
        D017 A 1 A_PlaySound("Don_HadesElemental/Teleport",CHAN_WEAPON)
        D017 A 1 A_SetTranslucent(0.90)
        D017 A 1 A_SetTranslucent(0.80)
        D017 A 1 A_SetTranslucent(0.70)
        D017 A 1 A_SetTranslucent(0.60)
        D017 A 1 A_SetTranslucent(0.50)
        D017 A 1 A_SetTranslucent(0.40)
        D017 A 1 A_SetTranslucent(0.30)
        D017 A 1 A_SetTranslucent(0.20)
        D017 A 1 A_SetTranslucent(0.10)
        D017 A 1 A_SetTranslucent(0.0)
        TNT1 A 0 A_Jump(12,31)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		TNT1 A 2
        TNT1 A 0 A_Jump(32,31)
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
        D017 A 1 A_PlaySound("Don_HadesElemental/Teleport",CHAN_WEAPON)
        D017 A 1 A_SetTranslucent(0.10)
        D017 A 1 A_SetTranslucent(0.20)
        D017 A 1 A_SetTranslucent(0.30)
        D017 A 1 A_SetTranslucent(0.40)
        D017 A 1 A_SetTranslucent(0.50)
        D017 A 1 A_SetTranslucent(0.60)
        D017 A 1 A_SetTranslucent(0.70)
        D017 A 1 A_SetTranslucent(0.80)
        D017 A 1 A_SetTranslucent(0.90)
        D017 A 1 A_SetTranslucent(1.0)
        D017 A 0 A_SetShootable
	    D017 A 0 A_ChangeFlag(NOPAIN,0)
        Goto See	
	Pain:
		D017 H 3
		D017 H 3 A_Pain
		D017 H 6
		Goto See	
	Death:
	    D017 I 5 A_Scream
        D017 JK 5 Bright
		D017 L 5 Bright A_NoBlocking	
        D017 L 0 A_SpawnItemEx("Don_HadesSphere", 0, 0, 10, 0, 0, 0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
        D017 MNO 5 Bright 
        D017 P 5  
        D017 QRSTU 4
        D017 V -1 A_SetFloorClip
        Stop 	
	}
}

ACTOR Don_HadesElemental2 : Don_HadesElemental
{   
    States
    {
    Spawn:
		D017 U 0
		D017 U 0 A_JumpIfInventory("Don_HadesElementalCheck", 1, 17)
		D017 U 0 A_ChangeFlag("NOBLOCKMAP", 1)
		D017 U 0 A_ChangeFlag("NOPAIN", 1)	
        D017 UTSRQP 2 
        D017 O 2 A_GiveInventory("Don_HadesElementalCheck",1)
        D017 NMLKJI 3
		D017 A 0 A_ChangeFlag("NOBLOCKMAP", 0)
		D017 A 0 A_ChangeFlag("NOPAIN", 0)	
		D017 A 10 A_Look
        Goto See
	}
}

ACTOR Don_HadesElementalBall
{  
	Height 8
	Radius 8
	Damage 4
    Speed 26
    FastSpeed 34
	DeathSound "Don_HadesElemental/Hit"
	Obituary "%o was electrocuted by a Hades Elemental."
	Decal "Don_HadesElemental_ScorchBall"
	Renderstyle Add
	Projectile
	
	States
	{
    Spawn:
		D046 ABC 2 Bright A_SpawnItemEx("Don_HadesElementalBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Death:
		D046 DEFGHI 3 Bright
	    Stop
    }
}

ACTOR Don_HadesElementalBallTrail
{
    Radius 0
    Height 1
    Speed 0
	Scale 0.85
    Projectile
    RenderStyle Add
	
    States
    {
    Spawn:
	    TNT1 A 1
        D047 ABCDEFHIJ 2 Bright
        Stop
    }
}

ACTOR Don_HadesElementalCheck : Inventory
{
    Inventory.Maxamount 1
}