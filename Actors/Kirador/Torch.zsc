
class KiradorTorch : CustomInventory
{
	Default
	{
		+FLOATBOB
		+INVENTORY.INVBAR
		Inventory.PickupMessage "$TXT_ARTITORCH";
		Tag "Torch";
		Inventory.Icon "KRERTRCH";
	}
	int user_brightness;
	int user_extrabrightness;
	CVar cv;
	override void PostBeginPlay()
	{
		cv=CVar.FindCvar("gl_lights");
		Super.PostBeginPlay();
		user_brightness=user_brightness? user_brightness:6;
		user_extrabrightness=user_extrabrightness? user_extrabrightness:4;
	}
	override void DoEffect()
	{
		Super.DoEffect();
		if(cv)
		{
			bool lights=cv.GetBool();
			if(lights && tracer)
			tracer.Destroy();
		}
		else
		cv=CVar.FindCvar("gl_lights");
	}
	States
	{
	Spawn:
		TRCH ABC 3 Bright;
		Loop;
	Use:
		TNT1 A 0
		{
			bool lights=invoker.cv.GetBool();
			if(invoker.tracer)
			{
				invoker.tracer.Destroy();
				A_Print("Torch off");
			}
			else if(!invoker.tracer && !lights)
			{
				A_Print("Torch on");
				A_GiveInventory("PowerKiradorTorch");
				invoker.tracer=FindInventory("PowerKiradorTorch");
				invoker.tracer.special1=invoker.user_brightness;
				invoker.tracer.special2=invoker.user_extrabrightness;
			}
		}
		Fail;
	}
}
Class PowerKiradorTorch : Powerup
{
	Default
	{
		Inventory.Icon "KRERTRCH";
		Powerup.Duration 0x7fffffff;
	}
	int NewTorch, NewTorchDelta;
	override void DoEffect ()
	{
		if (Owner == NULL || Owner.player == NULL)
		{
			return;
		}

		let player = Owner.player;
		if (EffectTics <= BLINKTHRESHOLD || player.fixedcolormap >= PlayerInfo.NUMCOLORMAPS)
		{
			Super.DoEffect ();
		}
		else 
		{
			Powerup.DoEffect ();
			player.extralight=special1+abs(sin(level.maptime*1.5))*special2;
		}
	}
	override void EndEffect ()
	{
		Super.EndEffect();
		if (Owner != NULL && Owner.player != NULL && Owner.player.fixedcolormap < PlayerInfo.NUMCOLORMAPS)
		{
			Owner.player.extralight = 0;
		}
	}
}