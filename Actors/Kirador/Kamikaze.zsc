Class KamikaziGuy : Actor //10300 - remember to move this on your mapinfo - ozy81
{
	Default
	{
		//$Category Realm667 Monsters
		Health 20;
		Radius 16;
		Height 56;
		Mass 100;
		Speed 12;
		MaxStepHeight 32;
		MaxDropOffHeight 32;
		PainChance 128;
		MONSTER;
		+FLOORCLIP
		+NOVERTICALMELEERANGE
		+QUICKTORETALIATE
		Painsound "Kamikazi/Pain";
		Obituary "%o fell victim to a beheaded kamikazi!";
	}

	States
	{
	Spawn:
		BKAM A 0 A_StopSound(CHAN_5);
		BKAM A 10 A_LookEx(0, 0, 896, 0, 45);
		Wait;
	Idle.Noise:
		BKAM A 0 A_StartSound("kamikazi/idle");
	Idle:
		BKAM A 0 A_StopSound(CHAN_5);
		BKAM A 1 A_Wander();
		BKAM AAA 1 A_LookEx(0, 0, 896, 0, 45);
		BKAM B 1 A_Wander();
		BKAM BBB 1 A_LookEx(0, 0, 896, 0, 45);
		BKAM C 1 A_Wander();
		BKAM CCC 1 A_LookEx(0, 0, 896, 0, 45);
		BKAM D 1 A_Wander();
		BKAM DDD 1 A_LookEx(0, 0, 896, 0, 45);
		BKAM A 0 A_Jump(8, "IdleNoise");
		Loop;
	See:
		BKAM A 0 A_StartSound("kamikazi/scream", CHAN_5, CHANF_LOOP);
	See.Chase:
		/*BKAM A 1 A_Chase();
		BKAM AAA 1 A_Chase2();
		BKAM B 1 A_Chase();
		BKAM BBB 1 A_Chase2();
		BKAM C 1 A_Chase();
		BKAM CCC 1 A_Chase2();
		BKAM D 1 A_Chase();
		BKAM DDD 1 A_Chase2();*/
		BKAM ABC 3
		{
			A_Chase();
			If(target && A_CheckFloor("Null")/* && CheckSight(target) && CheckMove(RotateVector((0,5),Angle)) */)
			{
				A_FaceTarget();
				A_Recoil(-3);
			}
		}
		Goto See+1;
	Pain:
		BKAM A 20
		{
			A_Pain();
			if(!A_CheckFloor("Null")) Return;
			A_Recoil(5);
			Vel.Z+=6;
		}
		Goto See;
	Melee:
		BKAM A 0 A_KamikaziDie();
		Stop;
	Death:
		BMBE M 0 A_KamikaziExplode();
		BMBE M 4 BRIGHT A_StartSound("kamikazi/explode",CHAN_WEAPON,0,1.0,0.5);
		BMBE N 4 BRIGHT;
		BMBE O 4 A_NoBlocking();
		BMBE PQRST 4;
		BMBE U -1;
		Stop;
	}
	
	// will be called frequently to simulate smoother movement and to check Idle calls - Nash & Ozy81
	protected void A_Chase2(void)
	{
	if ( A_CheckSightOrRange(1024, "Idle.Noise", TRUE) )
		{
			A_ClearTarget();
			SetIdle();
		}
	else
		{
			A_Chase('Melee', NULL, CHF_NOPLAYACTIVE);
		}
	}
	
	// trigger melee exploding attack to check if targets are dead - Ozy81
	protected void A_KamikaziDie(void)
	{
	
	if (target.health <= 0)
		{
			return;
		}
	else
		{
			A_Die();
		}
	}
	
	// unified exploding death states into one function - Ozy81
	protected void A_KamikaziExplode(void)
	{
		Actor mo=Spawn("KiradorRocketExplosion",pos+(0,0,40),ALLOW_REPLACE);
		mo.scale*=1.5;
		A_QuakeEx(1.4,1.4,1.4,20,0,1024,"",QF_SCALEDOWN);
		A_Explode(50, 96, XF_NOTMISSILE|XF_THRUSTLESS);
		A_Stop();
		A_StopSound(CHAN_5);
	}
}