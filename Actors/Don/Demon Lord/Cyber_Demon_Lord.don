//===========================================================================
//
//  Cyber Demon Lord
//
//===========================================================================

ACTOR Don_CyberDemonLord : Don_DemonLord 1214
{
	//$Category "Don Monsters"
	
	Tag "Cyber Demon Lord"
	
	Health 200
	PainChance 150
	Speed 20
	MaxTargetRange 280
  	Obituary "%o was munched by a cyber demon lord."
	
	States
	{
	Spawn:
		D014 AB 10 A_Look
		Loop
	See:		
		D014 AA 2 A_Chase
        D014 BB 2 A_Chase
		D014 B 0 A_PlaySound("Don_Demon2/Step")
        D014 CC 2 A_Chase
        D014 DD 2 A_Chase
		D014 D 0 A_PlaySound("Don_Demon2/Step")
		Loop
	Melee:
		D014 EF 5 A_FaceTarget
        D014 G 6 A_CustomMeleeAttack(FRandom(1,10)*4)
        Goto See
	Missile:
	    D014 E 0 A_PlaySound("Don_DemonLord/Melee2")
	    D014 EF 7 A_FaceTarget
	    D014 F 0 A_ChangeFlag(SLIDESONWALLS,1)
	    D014 F 0 ThrustThingZ(0,28,0,1)
	    D014 F 2 A_Recoil(-16)
	    D014 F 3 A_CustomMissile("Don_DemonLordLunge",18,0,0,CMF_AIMDIRECTION)
	    D014 GGG 2 A_Recoil(-5)
        D014 G 1 A_Recoil(1)
		D014 G 1 A_JumpIfCloser(71,"MeleeLand")
	    D014 G 0 A_CheckFloor("DemonLordLand")
	    Goto Missile+11
	DemonLordLand:
        D014 A 0 A_ScaleVelocity(0.6)	
        D014 A 0 A_PlaySound("Don_Body/Fall",5,0.75)
		D014 AA 2
        D014 BB 2
		D014 B 0 A_PlaySound("Don_Demon2/Step")
        D014 CC 2
        D014 DD 2
		D014 D 0 A_PlaySound("Don_Demon2/Step")
		D014 D 0 A_ScaleVelocity(0.0)
		D014 D 0 A_ChangeFlag(SLIDESONWALLS,0)
        Goto See
	MeleeLand:
		D014 E 1 A_FaceTarget
		D014 E 1 A_PlaySound("Don_DemonLord/Melee1")
		D014 E 1 A_ScaleVelocity(0.6)
		D014 E 2 A_ChangeFlag(SLIDESONWALLS,0)
        D014 F 5 A_FaceTarget
        D014 G 6 A_CustomMeleeAttack(FRandom(1,10)*4)
		D014 G 0 A_ScaleVelocity(0.0)
        Goto See
	Pain:
		D014 H 2
		D014 H 2 A_Pain
		Goto See
	Death:
		D014 I 6
		D014 J 6 A_Scream
		D014 K 5
		D014 K 0 A_PlaySound("Don_Body2/Fall",CHAN_BODY)
		D014 L 5 A_NoBlocking
		D014 M 5
		D014 N -1
		Stop
	Raise:
		D014 MLK 5
		D014 JI 6
		Goto See
	}
}

ACTOR Don_DemonLordLunge
{
    Height 15
    Radius 15
    Speed 30
    Damage (FRandom(1,10)*4)
    Projectile
    
	+DONTBLAST
	+DONTREFLECT
	
	States
    {
    Spawn:
	    TNT1 A 6
        Stop
    }
}