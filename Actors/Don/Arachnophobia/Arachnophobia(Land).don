//===========================================================================
//
//  Arachnophobia Land
//
//===========================================================================

ACTOR Don_Arachnophobia1 1225
{
	//$Category "Don Monsters"
	
	Tag "Arachnophobia"
	
	Health 625
    Radius 48
	Height 64
	Mass 850
	Speed 12
	Monster
	PainChance 96
	SeeSound "Don_Arachnophobia/Sight"
	PainSound "Don_Arachnophobia/Pain"
	ActiveSound "Don_Arachnophobia/Active"
	DeathSound "Don_Arachnophobia/Death"
	Obituary "%o was shocked by an Arachnophobia."
	Species "Spider"
	
	+FLOORCLIP
	+BOSSDEATH
	+MISSILEMORE
	+QUICKTORETALIATE
	
	States
	{
	Spawn:
		D025 AB 10 A_Look
		Loop
	See:
		D025 A 18 
		D025 A 3 A_PlaySound("Don_Arachnophobia/Step")
        D025 ABBCC 3 A_Chase
        D025 D 3 A_PlaySound("Don_Arachnophobia/Step")
        D025 DEEFF 3 A_Chase
		Goto See+1
	Missile:
		D025 A 0 A_FaceTarget
	    D025 A 6 A_PlaySound("Don_Arachnophobia/Charge1",CHAN_ITEM)
	    D025 A 0 A_Jump(64,"ShotA1")
		D025 A 6 A_FaceTarget
	    D025 A 0 A_Jump(64,"ShotA2")
		D025 A 6 A_FaceTarget
	    D025 A 0 A_Jump(64,"ShotA3")
		D025 A 6 A_FaceTarget
	    D025 A 0 A_Jump(32,"ShotA4")
		D025 A 6 A_FaceTarget
	    D025 A 0 A_Jump(32,"ShotB1")
		D025 A 6 A_FaceTarget		
	    D025 A 0 A_Jump(32,"ShotB2")
		D025 A 6 A_FaceTarget			
		Goto ShotB3
	ShotA1:
		D025 G 6 Bright A_FaceTarget
		D025 H 0 A_FaceTarget
		D025 H 0 A_StopSound(CHAN_ITEM)
		D025 H 0 A_PlaySound("Don_Arachnophobia/Fire1",CHAN_WEAPON)
		D025 H 6 Bright A_CustomMissile("Don_ArachnophobiaShotA1", 23, 0, FRandom(-3,3), 0)
		D025 G 6 Bright
		D025 A 10
		Goto See+1
	ShotA2:
		D025 G 6 Bright A_FaceTarget
		D025 H 0 A_FaceTarget
		D025 H 0 A_StopSound(CHAN_ITEM)
		D025 H 0 A_PlaySound("Don_Arachnophobia/Fire1",CHAN_WEAPON)
		D025 H 6 Bright A_CustomMissile("Don_ArachnophobiaShotA2", 23, 0, FRandom(-2,2), 0)
		D025 G 6 Bright
		D025 A 10
		Goto See+1
	ShotA3:
		D025 G 6 Bright A_FaceTarget
		D025 H 0 A_FaceTarget
		D025 H 0 A_StopSound(CHAN_ITEM)
		D025 H 0 A_PlaySound("Don_Arachnophobia/Fire1",CHAN_WEAPON)
		D025 H 6 Bright A_CustomMissile("Don_ArachnophobiaShotA3", 23, 0, FRandom(-1,1), 0)
		D025 G 6 Bright
		D025 A 10
		Goto See+1
	ShotA4:
		D025 G 6 Bright A_FaceTarget
		D025 H 0 A_FaceTarget
		D025 H 0 A_StopSound(CHAN_ITEM)
		D025 H 0 A_PlaySound("Don_Arachnophobia/Fire1",CHAN_WEAPON)
		D025 H 6 Bright A_CustomMissile("Don_ArachnophobiaShotA4", 23, 0, 0, 0)
		D025 G 6 Bright
		D025 A 10
		Goto See+1
	ShotB1:
		D025 I 6 Bright A_FaceTarget
		D025 J 0 A_FaceTarget
		D025 J 0 A_StopSound(CHAN_ITEM)
		D025 J 0 A_PlaySound("Don_Arachnophobia/Fire2",CHAN_WEAPON)
		D025 J 6 Bright A_CustomMissile("Don_ArachnophobiaShotB1", 23, 0, FRandom(-2,2), 0)
		D025 I 6 Bright
		D025 A 10
		Goto See+1
	ShotB2:
		D025 I 6 Bright A_FaceTarget
		D025 J 0 A_FaceTarget
		D025 J 0 A_StopSound(CHAN_ITEM)
		D025 J 0 A_PlaySound("Don_Arachnophobia/Fire2",CHAN_WEAPON)
		D025 J 6 Bright A_CustomMissile("Don_ArachnophobiaShotB2", 23, 0, FRandom(-1,1), 0)
		D025 I 6 Bright
		D025 A 10
		Goto See+1
	ShotB3:
		D025 I 6 Bright A_FaceTarget
		D025 J 0 A_FaceTarget
		D025 J 0 A_StopSound(CHAN_ITEM)
		D025 J 0 A_PlaySound("Don_Arachnophobia/Fire2",CHAN_WEAPON)
		D025 J 6 Bright A_CustomMissile("Don_ArachnophobiaShotB3", 23, 0, 0, 0)
		D025 I 6 Bright
		D025 A 10
		Goto See+1
	Pain:
		D025 K 3 
		D025 K 0 A_StopSound(CHAN_ITEM)
		D025 K 3 A_Pain
		Goto See+1
	Death:
		D025 L 2 A_StopSound(CHAN_ITEM)
	    D025 L 2 A_Scream
		D025 LLLLLLLL 2 A_SpawnItemEx("Don_BloodFly1", FRandom(-15,15), FRandom(-15,15), FRandom(32,42), FRandom(5,-5), FRandom(5,-5), FRandom(4,8))  
   	    D025 M 6 A_NoBlocking
   	    D025 NOP 6
    	D025 Q 6 A_PlaySound("Don_Body2/Fall",CHAN_BODY)
    	D025 R -1 A_BossDeath
		Stop
    Raise:
		D025 QPONML 6
		Goto See+1
	}
}

ACTOR Don_ArachnophobiaShotA1 : FastProjectile
{
	Speed 90
	Damage (30)
	Radius 5
	Height 5
	Scale 0.24
	Projectile
	Renderstyle Add
	SeeSound "Don_Arachnophobia/Projectile1"
	DeathSound "Don_Arachnophobia/Explode1"
	Decal "RailScorchLower"
	
    +FOILINVUL
	+RANDOMIZE
    +DONTREFLECT
    +BLOODSPLATTER
	
	States
	{
	Spawn:
		D057 A 1 Bright A_SpawnItemEx("Don_ArachnophobiaShot1Trail1", -(MomX/1.2), -(MomY/1.2), (MomZ/1.2*-1), (MomX/1.2), (MomY/1.2), (MomZ/1.2), 0, 138)
		D057 A 0 A_SpawnItemEx("Don_ArachnophobiaShot1Trail2",0,0,0,0,0,0)
		Loop 
	Death:
	    D057 B 0
		D057 B 0 A_SetScale(0.42,0.42)
		D057 BCDEFGHIJKLMNOPQR 1 Bright
		Stop
	}
}

ACTOR Don_ArachnophobiaShotA2 : Don_ArachnophobiaShotA1
{
	Speed 100
	Damage (35)
}

ACTOR Don_ArachnophobiaShotA3 : Don_ArachnophobiaShotA1
{
	Speed 110
	Damage (40)
}

ACTOR Don_ArachnophobiaShotA4 : Don_ArachnophobiaShotA1
{
	Speed 120
	Damage (45)
}

ACTOR Don_ArachnophobiaShotB1 : Don_ArachnophobiaShotA1
{
	Speed 130
	Damage (50)
	Radius 7
	Height 7
	SeeSound "Don_Arachnophobia/Projectile2"
	DeathSound "Don_Arachnophobia/Explode2"
	Decal "CrossbowScorch2"
	
	+EXTREMEDEATH
	
	States
	{
	Spawn:
		D058 A 1 Bright A_SpawnItemEx("Don_ArachnophobiaShot2Trail1", -(MomX/1.2), -(MomY/1.2), (MomZ/1.2*-1), (MomX/1.2), (MomY/1.2), (MomZ/1.2), 0, 138)
		D058 A 0 A_SpawnItemEx("Don_ArachnophobiaShot2Trail2",0,0,0,0,0,0)
		Loop 
	Death:
	    D058 B 0
		D058 B 0 A_SetScale(0.65,0.65)
		D058 BCDEFGHIJKLMNOPQ 2 Bright
		Stop
	}
}

ACTOR Don_ArachnophobiaShotB2 : Don_ArachnophobiaShotB1
{
	Speed 140
	Damage (55)
}

ACTOR Don_ArachnophobiaShotB3 : Don_ArachnophobiaShotB1
{
	Speed 150
	Damage (60)
}

ACTOR Don_ArachnophobiaShot1Trail1
{
	Radius 1
    Height 1
	RenderStyle Add
	Scale 0.24
	
	+NOGRAVITY
    +DONTSPLASH
	+NOINTERACTION
    +CLIENTSIDEONLY
	
	States
	{
	Spawn:
	    D057 A 1 Bright A_FadeOut(0.08)
		Stop
	}
}

ACTOR Don_ArachnophobiaShot2Trail1 : Don_ArachnophobiaShot1Trail1
{
	States
	{
	Spawn:
	    D058 A 0 
	    D058 A 1 Bright A_FadeOut(0.08)
		Stop
	}
}

ACTOR Don_ArachnophobiaShot1Trail2 : Don_ArachnophobiaShot1Trail1
{
	Scale 0.36
	States
	{
	Spawn:
	    TNT1 A 2 
	    D059 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	}
}

ACTOR Don_ArachnophobiaShot2Trail2 : Don_ArachnophobiaShot1Trail1
{
	Scale 0.36
	States
	{
	Spawn:
	    TNT1 A 2
	    D060 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	}
}